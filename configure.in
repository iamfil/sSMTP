dnl Process this file with autoconf to produce a configure script.
AC_INIT(ssmtp.c)

if test x"$CFLAGS" = x
then
	CFLAGS="-Wall -Wextra -Werror -std=c99 -pedantic"
fi

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h strings.h syslog.h unistd.h)


AC_CACHE_CHECK([for obsolete openlog],ssmtp_cv_obsolete_openlog,
	[ AC_TRY_COMPILE([#include <syslog.h> ] , [ openlog("xx",1); ] ,
	ssmtp_cv_obsolete_openlog=yes, ssmtp_cv_obsolete_openlog=no)]
)


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_STRUCT_TM

dnl Checks for libraries.
AC_SEARCH_LIBS(gethostname, nsl)
AC_SEARCH_LIBS(socket, socket)
AC_SEARCH_LIBS(initscr, ncurses)

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gethostname socket strdup strndup strstr)

savedCFLAGS=$CFLAGS
AC_CHECK_DECL(isascii, [],
	unset ac_cv_have_decl_isascii
	CFLAGS="$CFLAGS -D_BSD_SOURCE"
	AC_CHECK_DECL(isascii, [AC_DEFINE(_BSD_SOURCE)],
		[AC_ERROR(no isascii)], [#include <ctype.h>]),
	[#include <ctype.h>])
CFLAGS=$savedCFLAGS

dnl Check for optional features
AC_ARG_ENABLE(logfile,
[  --enable-logfile        additional logging over and above syslog])
if test x$enableval = xyes ; then
	AC_DEFINE(LOGFILE)
fi
enableval=""

AC_ARG_ENABLE(rewrite-domain,
[  --disable-rewrite-domain
                          support for rewriting the sending domain])
if test x$enableval != xno ; then
	AC_DEFINE(REWRITE_DOMAIN)
fi
enableval=""

# CUnit requires ncurses
if test x$ac_cv_search_initscr != xno ; then
	AC_ARG_ENABLE([cunit], [  --enable-cunit=<path> support cunit testing],[
		savedCFLAGS=$CFLAGS
		CFLAGS="$CFLAGS -L$enableval/lib -I$enableval/include"
		AC_CHECK_LIB([cunit], [CU_get_error], [
			AC_CHECK_HEADER([CUnit/CUnit.h], [
				savedCFLAGS=$CFLAGS
				TEST=1
				AC_SUBST(TEST)
				LIBS="$LIBS -lcunit"
			])
		])
		enableval=""
		CFLAGS=$savedCFLAGS
	])
fi

AC_ARG_ENABLE(ssl,
[  --enable-ssl           support for secure connection to mail server])
if test x$enableval = xyes ; then
	AC_CHECK_HEADER(openssl/crypto.h, [
		AC_DEFINE(HAVE_SSL)
		LIBS="$LIBS -lssl"
		AC_CHECK_LIB(crypto, X509_free, LIBS="$LIBS -lcrypto")])
fi
enableval=""

savedCFLAGS=$CFLAGS
if test "x$ac_cv_header_openssl_crypto_h" == "xyes"; then
	AC_MSG_CHECKING(SSL_read)

	AC_LANG_PUSH(C)
	AC_COMPILE_IFELSE(
		AC_LANG_PROGRAM(
			[#include <openssl/ssl.h>],
			[SSL_read(NULL, NULL, 0);]
		), [
			ac_cv_func_SSL_read=yes
			AC_MSG_RESULT(yes)
		], ac_cv_func_SSL_read=no
	)

	if test "x$ac_cv_func_SSL_read" != "xyes"; then
		CFLAGS="$CFLAGS -Wno-deprecated-declarations"
		AC_COMPILE_IFELSE(
			AC_LANG_PROGRAM(
				[#include <openssl/ssl.h>],
				[SSL_read(NULL, NULL, 0);]
			), [
				ac_cv_func_SSL_read=yes
				savedCFLAGS=$CFLAGS
				AC_MSG_RESULT([yes, with -Wno-deprecated-declarations])
			], AC_MSG_FAILURE(missing)
		)
	fi

	AC_LANG_POP()
fi
CFLAGS=$savedCFLAGS

savedLIBS=$LIBS
LIBS=
AC_ARG_ENABLE(sasl,
[  --enable-sasl          support for SASL authentication])
if test x$enableval = xyes ; then
	AC_CHECK_HEADER(sasl/sasl.h, [
		AC_CHECK_LIB(sasl2, sasl_decode64, [
			AC_DEFINE(HAVE_SASL)
			savedLIBS="$savedLIBS -lsasl2" ])])
fi
enableval=""
LIBS=$savedLIBS

if test "x$ac_cv_lib_sasl2_sasl_decode64" != xyes ; then
	SRCS="$SRCS base64.c"
fi

AC_ARG_ENABLE(inet6,
[  --enable-inet6         support for IPv6 transport])
if test x$enableval = xyes ; then
	AC_DEFINE(INET6)
fi
enableval=""

AC_ARG_ENABLE(md5auth,
[  --enable-md5auth         support for MD5 authentication])
if test x$enableval = xyes ; then
	AC_DEFINE(MD5AUTH)
	SRCS="$SRCS md5auth/md5c.c md5auth/hmac_md5.c"
fi
enableval=""

AC_ARG_ENABLE(debug,
[  --enable-debug          enable debug output])
if test x$enableval = xyes ; then
	AC_DEFINE(DEBUG)
fi
enableval=""

AC_SUBST(TEST)
AC_SUBST(LIBRARY_PATH)
AC_SUBST(SRCS)
AC_SUBST(LIBS)
AC_OUTPUT(Makefile)
